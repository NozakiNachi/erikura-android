apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'realm-android'

apply plugin: 'com.google.gms.google-services'

apply plugin: 'io.fabric'

// MEMO: Google Fit API を使うために、OAuth IDの登録を行う
// 1. 以下のURLに移動し、アプリケーションを登録する
//    https://console.developers.google.com/flows/enableapi?apiid=fitness
// 2. OAuth クライアント IDを作成します
//    - アプリケーションの種類: android
//    - 名前: テスト用には適当な名前を設定
//    - フィンガープリント： 以下の手順で取得
//      * keytool -keystore ~/.android/debug.keystore -list -v
//      うまくいかない時
//      * keytool -list -keystore "C:\Users\<ユーザ名>\.android\debug.keystore" -v
//      * SHA-1 の値をコピー
//    - パッケージ名: AndroidManifest.xml より

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "jp.co.recruit.erikura"
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        def properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())

        // MEMO: http でアクセスするためには res/xml/network_security_config.html にドメインを追加してください
        buildConfigField("String", "SERVER_BASE_URL", '"' + properties.getProperty("server.base_url") + '"')
        buildConfigField("String", "GOOGLE_MAP_API_KEY", '"' + properties.getProperty("google.map_api_key") + '"')
        buildConfigField("String", "GEOCODING_API_KEY", '"' + properties.getProperty("google.geocoding_api_key") + '"')
        // FIXME: geocoding 用のURL
    }
    buildTypes {
        debug {
            buildConfigField("String", "KARTE_APP_KEY", '"3GIormvTYdFdV3ZxncnkMhtfGrMipNUc"')
            buildConfigField("String", "ADJUST_CLASS", '"ADJEnvironmentSandbox"')
            buildConfigField("String", "ADJUST_KEY", '"4k5awo8maj5s"')
        }
        staging {
            buildConfigField("String", "SERVER_BASE_URL", "\"https://dev-app.erikura.net\"")
            buildConfigField("String", "KARTE_APP_KEY", '"3GIormvTYdFdV3ZxncnkMhtfGrMipNUc"')
            buildConfigField("String", "ADJUST_CLASS", '"ADJEnvironmentSandbox"')
            buildConfigField("String", "ADJUST_KEY", '"4k5awo8maj5s"')
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField("String", "SERVER_BASE_URL", "\"https://app.erikura.net\"")
            buildConfigField("String", "KARTE_APP_KEY", '"Fnz0u2qNxnY4BmYLtH1LBHOJjUjTgA7Q"')
            buildConfigField("String", "ADJUST_CLASS", '"ADJEnvironmentProduction"')
            buildConfigField("String", "ADJUST_KEY", '"4k5awo8maj5s"')
        }
    }
    dataBinding {
        enabled = true
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    jcenter()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // LiveData
    def lifecycle_version = '2.2.0'
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"

    // Google Play Services
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation 'com.google.android.gms:play-services-fitness:18.0.0'
    implementation 'com.google.android.gms:play-services-auth:17.0.0'
    implementation 'com.google.android.gms:play-services-location:17.0.0'

    // Firebase
    implementation 'com.google.firebase:firebase-analytics:17.2.2'
    implementation 'com.google.firebase:firebase-messaging:20.1.0'
    // Karte
    implementation 'io.karte.android:tracker:1.7.+'
//    // Facebook SDK Core only (Analytics)
//    implementation 'com.facebook.android:facebook-core:5.+'
//    // Adjust
//    implementation 'com.adjust.sdk:adjust-android:4.20.0'

    // Dagger
    def dagger_version = '2.26'
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    // ReactiveJava
    implementation "io.reactivex.rxjava2:rxjava:2.2.16"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation "io.reactivex.rxjava2:rxkotlin:2.4.0"

    // Retrofit
    def retrofit_version = '2.7.1'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    // OkHTTP
    implementation 'com.squareup.okhttp3:logging-interceptor:4.3.1'


    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.maps.android:android-maps-utils:0.6.2'
    implementation 'commons-io:commons-io:2.6'
    implementation 'commons-lang:commons-lang:2.6'
    implementation 'com.android.support:design:28.0.0'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'

    implementation('com.github.bumptech.glide:glide:4.11.0') {
        exclude group: "com.android.support"
    }
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
//    implementation "com.zhihu.android:matisse:0.5.2"
}

androidExtensions {
    experimental = true
}
